plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = '1.8'

    group 'fr.minuskube'
    version '1.0.0'

    configurations { provided }

    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.25'
        compile 'org.slf4j:slf4j-simple:1.7.25'

        testCompile 'junit:junit:4.12'
    }

    task copyJar(type: Copy, dependsOn: shadowJar) {
        from shadowJar
        into '../build'
    }

    compileJava.finalizedBy copyJar
}

project(':core') {
    shadowJar {
        archiveName = 'Netherboard-API-' + project.version + '.jar'
    }
}

project(':bukkit') {
    shadowJar {
        archiveName = 'Netherboard-Bukkit-' + project.version + '.jar'
    }

    repositories {
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        provided 'org.spigotmc:spigot-api:1.11.2-R0.1-SNAPSHOT'

        compile project(':core')
    }
}

project(':sponge') {
    shadowJar {
        archiveName = 'Netherboard-Sponge-' + project.version + '.jar'
    }

    repositories {
        maven { url 'https://repo.spongepowered.org/maven' }
    }

    dependencies {
        provided 'org.spongepowered:spongeapi:5.1.0'

        compile project(':core')
    }
}