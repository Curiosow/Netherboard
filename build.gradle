plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java-library'

    sourceCompatibility = '1.8'

    group 'fr.minuskube'
    version '1.2.3'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.25'
        implementation 'org.slf4j:slf4j-simple:1.7.25'
    }

    task copyJar(type: Copy, dependsOn: shadowJar) {
        from shadowJar
        into '../build'
    }

    shadowJar.finalizedBy copyJar
}

project(':core') {
    shadowJar {
        archiveFileName = 'Netherboard-API-' + project.version + '.jar'
    }

    archivesBaseName = 'netherboard-core'
}

project(':bukkit') {
    shadowJar {
        archiveFileName = 'Netherboard-Bukkit-' + project.version + '.jar'
    }

    archivesBaseName = 'netherboard-bukkit'

    repositories {
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.11.2-R0.1-SNAPSHOT'

        api project(':core')
    }
}

project(':sponge') {
    shadowJar {
        archiveFileName = 'Netherboard-Sponge-' + project.version + '.jar'
    }

    archivesBaseName = 'netherboard-sponge'

    repositories {
        maven { url 'https://repo.spongepowered.org/maven' }
    }

    dependencies {
        compileOnly 'org.spongepowered:spongeapi:5.1.0'

        api project(':core')
    }
}

subprojects {
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = archivesBaseName

                artifact tasks.jar
                artifact tasks.javadocJar
                artifact tasks.sourcesJar

                pom {
                    name = 'Netherboard'
                    description = 'Scoreboard API for your Minecraft Sponge and Bukkit Plugins.'
                    url = 'https://github.com/MinusKube/Netherboard'

                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    developers {
                        developer {
                            id = 'minuskube'
                            name = 'MinusKube'
                            email = 'minuskube@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/MinusKube/Netherboard.git'
                        developerConnection = 'scm:git:ssh://github.com:MinusKube/Netherboard.git'
                        url = 'http://github.com/MinusKube/Netherboard/tree/master'
                    }
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username project.ossrhUsername
                    password project.ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}